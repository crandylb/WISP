OUTPT,  TO ASSEM IF CAR T NE ATOM.      IS THE EXPRESSION A SINGLE OPERAND, NO.
        PRINT CAR T.                    YES, WRITE IT.
END,    PRINT '., RETURN.               TERMINATE THE EXPRESSION.
ASSEM,  TO BRNCH AND BACK, TO END.      PROCESS A COMPLEX EXPRESSION.
BRNCH,  TO SAVE IF CDR T NE NIL.        IS THIS THE FIRST OF A PAIR, YES.
        TO SUBXB AND BACK.              NO, MOVE ONTO THE OPERAND EXPRESSION.
        T = CDR T.                      STEP ALONG TO ITS FIRST OPERAND.
        TO SKIPA IF CAR T = ATOM.       IS IT A SIMPLE VARIABLE, YES.
        TO BRNCH AND BACK.              NO, PROCESS IT RECURSIVELY.
LOOK,   TO BACK IF CAR Y = NIL.         ARE THERE MORE TO BE FINISHED, NO.
        T = CAR Y, POP UP Y.            YES, GO TO THE MOST RECENT.
        PRINT CAR S.                    PUT OUT THE DEFERRED OPERATOR.
        TO SKIPB IF CAR T = ATOM.       IS IT A SIMPLE VARIABLE, YES.
        TO BRNCH AND BACK.              NO, PROCESS IT RECURSIVELY.
        POP UP S, TO LOOK IF CAR S NE ').
        PRINT CAR S, POP UP S, TO LOOK. CLOSE OFF A SUBEXPRESSION.
SAVE,   PUSH DOWN Y, CAR Y = CDR T.     SAVE THE SECOND OPERAND FOR LATER.
        TO SUBXA AND BACK.              MOVE ONTO THE FIRST OPERAND.
        T = CDR T.                      STEP ALONG TO ITS FIRST OPERAND.
        TO SKIPA IF CAR T = ATOM.       IS IT A SINGLE VARIABLE, YES.
        TO BRNCH AND BACK, TO LOOK.     NO, PROCESS IT RECURSIVELY.
SKIPA,  PRINT CAR T, PRINT CAR S.       PUT OUT THE VARIABLE AND OPERATOR.
        T = CDR T.                      MOVE TO THE SECOND OPERAND.
        TO SKIPB IF CAR T = ATOM.       IS IT A SIMPLE VARIABLE, YES.
        TO BRNCH AND BACK.              NO, PROCESS IT RECURSIVELY.
        POP UP S, TO LOOK IF CAR S NE ').
        PRINT CAR S, POP UP S, TO LOOK. CLOSE OFF A SUBEXPRESSION.
SKIPB,  PRINT CAR T.                    PUT OUT THE VARIABLE.
        POP UP S, TO BACK IF CAR S NE ').
        PRINT CAR S, POP UP S, RETURN.  PUT OUT A ).

SUBXA,  T = CAR T, TO OPRAT AND BACK.   DECIDE ON PAREN INSERTION.
        TO NOBR IF CAR X = 'I.               (FIRST SUBEXPRESSION)
        TO BACK IF CAR S = '$.
        TO NOBR IF CAR Q = '2.
        TO BRAK IF CAR S = '/.
        TO BRAK IF CAR S = '*.
        TO NOBR.
SUBXB,  T = CAR T, TO OPRAT AND BACK.   DECIDE ON PAREN INSERTION.
        TO NOBR IF CAR S = NIL.              (SECOND SUBEXPRESSION)
        TO BRAK IF CAR S = '$.
        TO NOBR IF CAR S = '+.
        TO BRAK IF CAR Q = '3.
        TO NOBR IF CAR S NE '/.
        TO NOBR IF CAR Q = '1.
BRAK,   PRINT '(, PUSH DOWN S, CAR S = ').   INSERT '(' AND STACK ')'.
NOBR,   PUSH DOWN S, CAR S = CAR T, RETURN.  STACK THE OPERATOR.
OPRAT,  W = 'O, Q = 'P, CAR X = 'I.  DECODE THE RELATIVE OPERATOR STRENGTH.
SCANA,  W = CDR W, Q = CDR Q, TO BACK IF CAR W = CAR T.
        TO SWTCH IF CAR W = CAR S.
        TO SCANA IF CDR W NE NIL, RETURN.
SWTCH,  CAR X = 'S, TO BACK IF CDR W = NIL.
SCANB,  W = CDR W, Q = CDR Q, TO BACK IF CAR W = CAR T.
        TO SCANB IF CDR W NE NIL, RETURN.
