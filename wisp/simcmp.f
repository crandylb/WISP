C  simcmp.f -- Simple Compiler in Fortran, from WISP, Feb, 1967, p95-6
      DIMENSION LIST(15000)
      COMMON LIST
      DATA KMAX/15000/
C  IF NO GENERATED LABELS ARE REQUIRED, DELETE THE FOLLOWING CARD
      LIST(6)=100
C
C     READ CONTROL CHARACTERS, IN THE ORDER -
C                 SOURCE EOL, SOURCE PARAM, MCT EOL, MCT PARAM, ZERO.
C
      DO 1 I=1,5
    1 LIST(I)=INPUT(1)
      K=16
C
C     READ MACRO DEFINITIONS
C
    2 J=INPUT(0)
C  IF NO GENERATED LABELS ARE REQUIRED, REPLEACE THE FOLLOWING THREE CARDS
C  WITH -
C     I=K
      L=K+1
      LIST(L)=-1
      I=L
    3 IF (I .GE. KMAX)  GO TO 100
      I=I+1
      LIST(I)=INPUT(1)
      IF (LIST(I) .NE. LIST(1))  GO TO 3
    6 J=INPUT(0)
      J=I+1
    7 I=I+1
      IF (I .GE. KMAX)  GO TO 100
      LIST(I)=INPUT(1)
      IF (LIST(I) .NE. LIST(4))  GO TO 12
      LIST(I)=LIST(5)-INPUT(1)-6
C  IF NO GENERATED LABELS ARE REQUIRED, DELETE THE FOLLOWING CARD.
      J=I
      I=I+1
      LIST(I)=INPUT(1)-LIST(5)
C  IF NO GENERATED LABELS ARE REQUIRED, DELETE THE FOLLOWING TWO CARDS.
      IF (LIST(J) .NE. (-6))  GO TO 7
      IF (LIST(I) .GT. LIST (L)) LIST(L)=LIST(I)
      GO TO 7
   12 IF (LIST(I) .NE. LIST(3))  GO TO 7
      LIST(I)=-1
      IF (I .GT. J)  GO TO 6
      LIST(K)=I
      K=I
      J=INPUT(1)
      IF (J .NE. LIST(3))  GO TO 2
C
C     READ A SOURCE STATEMENT
C
   20 J=INPUT(0)
      DO 21 I=K,KMAX
      LIST(I)=INPUT(1)
      IF (LIST(I) .EQ. LIST(1))  GO TO 30
   21 CONTINUE
      GO TO 100
C
C     TRANSLATE ONE STATEMENT
C
   30 IF (I .EQ. K) CALL EXIT
      M=16
   31 L=7
      DO 34 J=K,I
C  IF NO GENERATED LABELS ARE REQUIRED, REPLACE THE FOLLOWING CARD WITH-
C     N=J-K+M+1
      N=J-K+M+2
      IF (LIST(N) .EQ. LIST(2))  GO TO 33
      IF (LIST(N) .EQ. LIST(J))  GO TO 34
      M=LIST(M)
      IF(M-K)31,50,50
   33 LIST(L)=LIST(J)
      L=L+1
   34 CONTINUE
      J=N
      GO TO 41
C
C     PUNCH MACHINE CODE TRANSLATION
C
   40 L=-LIST(J)
      J=J+1
C  IF NO GENERATED LABELS ARE REQUIRED, DELETE THE FOLLOWING CARD
      IF (L .EQ. 6)  GO TO 46
      CALL PUNCH (LIST(L),LIST(J))
   41 J=J+1
      IF (LIST(J) .LT. (-1))  GO TO 40
      CALL PUNCH (LIST(J),0)
C  IF NO GENERATED LABELS ARE REQUIRED, REPLACE THE FOLLOWING 7 CARDS
C WITH -
C     IF (J+1-LIST(M)) 41,20,20
      IF (J+1 .LT. LIST(M))  GO TO 41
      M=M+1
      LIST(6)=LIST(6)+LIST(M)+1
      GO TO 20
   46 L=LIST(6)+LIST(J)
      CALL PUNCH(L,1)
      GO TO 41
C
C     PRINT OUT AN ERROR
C
   50 DO 51 J=K,I
   51 CALL PRINT(LIST(J),0)
      CALL PRINT(-1,0)
      GO TO 20
C
C     SYSTEM ERROR MESSAGES
C
  100 I=KMAX-200
      CALL DUMP(LIST(I),LIST(KMAX),0)
      END
